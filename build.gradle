// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '2.0.21'
    ext.nav_version = "2.8.4"
    ext.devkit_version = '2.0.0-1.2.0'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.1'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://jitpack.io' }
    }
}


ext {
    // Compile Settings
    COMPILE_SDK = 35 // temporarily for avoiding xml completion issue
    BUILD_TOOLS = '35.0.0'

    MINI_SDK_VERSION = 31
    TARGET_SDK_VERSION = 35

    VERSION_NAME = version()
    VERSION_CODE = versionCode().toInteger()

    if (file("core/apikey.gemini").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("core/apikey.gemini")))

        GEMINI_API_KEY = props.getProperty('API_KEY')
    } else {
        GEMINI_API_KEY = ""
    }

}


def String version() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    return versionProps['major'] + "." + versionProps['minor'] + "." + versionProps['patch']
}

def String versionCode() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    return versionProps['code']
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
